# Override any keybindings
bindkey -e

# mark tracking functions so we can properly handle
# Ctrl+g as unset mark if the mark is set
unset-mark-command () {
    zle set-mark-command -n -1
    MARKISSET=false
}
zle -N unset-mark-command

copy-region-as-kill-unmark () {
    zle copy-region-as-kill
    # also unset mark like it should
    zle unset-mark-command
    MARKISSET=false
}
zle -N copy-region-as-kill-unmark

kill-region-tracked () {
    zle kill-region
    MARKISSET=false
}
zle -N kill-region-tracked

set-mark-command-tracked () {
    zle set-mark-command
    MARKISSET=true
}
zle -N set-mark-command-tracked

# uses MARKISSET from other commands
unset-or-break-mark-command () {
    if ${MARKISSET} >/dev/null
    then
        zle unset-mark-command
    else
        zle send-break
    fi
}
zle -N unset-or-break-mark-command

# define functions to share clipboard with X11
# breaks yank-pop

# uses CUTBUFFER from x-yank
x-backward-kill-word () {
  zle backward-kill-word
  print -rn ${CUTBUFFER} | xsel -i
}
zle -N x-backward-kill-word

# uses CUTBUFFER from x-yank
x-copy-region-as-kill () {
  zle copy-region-as-kill
  print -rn ${CUTBUFFER} | xsel -i
}
zle -N x-copy-region-as-kill

# uses CUTBUFFER from x-yank
x-kill-region () {
  zle kill-region
  print -rn ${CUTBUFFER} | xsel -i
}
zle -N x-kill-region

x-yank () {
  CUTBUFFER=$(xsel -o </dev/null)
  zle yank
}
zle -N x-yank

# uses CUTBUFFER from x-yank
x-kill-line () {
  zle kill-line
  print -rn ${CUTBUFFER} | xsel -i
}
zle -N x-kill-line

# Alt+u
bindkey '^[u' beginning-of-line
# Alt+o
bindkey '^[o' end-of-line
# Alt+l
bindkey '^[l' forward-char
# Alt+Shift+l
bindkey '^[L' emacs-forward-word
# Alt+l
bindkey '^[j' backward-char
# Alt+Shift+j
bindkey '^[J' emacs-backward-word
# Ctrl+Backspace
bindkey '^^?' backward-kill-word
# Ctrl+w
bindkey '^w' kill-region-tracked
# Alt+w
bindkey '^[w' copy-region-as-kill-unmark
# Ctrl+k
bindkey '^k' kill-line
# Ctrl+y
bindkey '^y' yank
# Alt+y
bindkey '^[y' yank-pop
# Alt+space
bindkey '^[ ' set-mark-command-tracked
# Ctrl+space
bindkey '^ ' set-mark-command-tracked
# Ctrl+@ (what's actually sent on Ctrl+space)
bindkey '^@' set-mark-command-tracked
# Ctrl+Shift+-
bindkey '^_' undo
# Ctrl+x Ctrl+x
bindkey '^x^x' exchange-point-and-mark
# Ctrl+g
bindkey '^g' unset-or-break-mark-command

# zsh-fuzzy-search-and-edit plugin
bindkey '^f' fuzzy-search-and-edit

# zaw keybindings
bindkey '^r' zaw-history
bindkey '^[h' zaw-fasd
bindkey '^[gb' zaw-git-branches
bindkey '^[gl' zaw-git-log
bindkey '^t' zaw

if ! bindkey -l | grep -F -q 'filterselect' ; then
  if ! command -v zplug &>/dev/null; then
    echo >&2 "Missing zplug needed to add 'zaw'"
  elif ! zplug check "zsh-users/zaw"; then
    echo >&2 "Didn't load 'zaw' zplug with the rest"
  else
    echo >&2 "'zaw' zplug is reporting successful loading, but keymap isn't loaded"
  fi
else
  bindkey -M filterselect '^[k' down-line-or-history # Alt+k
  bindkey -M filterselect '^[i' up-line-or-history # Alt+i
  bindkey -M filterselect '^>' end-of-history # Ctrl+>
  bindkey -M filterselect '^<' beginning-of-history # Ctrl+<
  bindkey -M filterselect '^[j' select-action # Alt+j
  bindkey -M filterselect '^[u' beginning-of-line # Alt+u
  bindkey -M filterselect '^[o' end-of-line # Alt+o
  bindkey -M filterselect '^[l' forward-char # Alt+l
  bindkey -M filterselect '^[L' emacs-forward-word # Alt+Shift+l
  bindkey -M filterselect '^[j' backward-char # Alt+l
  bindkey -M filterselect '^[J' emacs-backward-word # Alt+Shift+j
  bindkey -M filterselect '^^?' backward-kill-word # Ctrl+Backspace
fi

# forgit binding
zle -N forgit::add
bindkey '^[gs' forgit::add